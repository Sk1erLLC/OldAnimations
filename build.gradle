buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven { url = "https://repo.spongepowered.org/maven" }
        flatDir dirs: 'libs'
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

group = "club.sk1er"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    clientJvmArgs += "-Dfml.coreMods.load=club.sk1er.oldanimations.tweaker.AnimationsTweaker"
}

repositories {
    jcenter()
    maven { url = "https://repo.spongepowered.org/maven" }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    provided files("libs/modcore.jar")
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
    }
}

jar {
    dependsOn configurations.embed
    from { configurations.embed.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes "TweakClass": 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.oldanimations.json',
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true,
                'ModSide': 'CLIENT',
                'FMLCorePlugin': 'club.sk1er.oldanimations.tweaker.AnimationsTweaker'
    }
}


mixin {
    add sourceSets.main, "mixins.oldanimations.refmap.json"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
